# Look at poky/meta-yocto/conf/distro/poky.conf for a baseline

DISTRO = "nilrt"
DISTRO_NAME = "NI Linux Real-Time"
DISTRO_VERSION = "5.0"
SDK_VENDOR = "-nilrtsdk"
SDK_VERSION := "${@'${DISTRO_VERSION}'}"

MAINTAINER = "NI Linux Real-Time Maintainers <nilrt@ni.com>"

TARGET_VENDOR = "-nilrt"

LOCALCONF_VERSION = "1"
LAYER_CONF_VERSION ?= "5"

# Name of the NILRT release which typically appears in documentation
# and the feed URI.
NILRT_RELEASE_NAME = "Cardassia"

DISTRO_FEATURES = "argp \
                   ext2 \
                   largefile \
                   usbgadget \
                   usbhost \
                   wifi \
                   xattr \
                   zeroconf \
                   pci \
                   sysvinit \
                   pam \
                   selinux \
                   x11 \
                   opengl \
                   ptest \
                   bluetooth \
                   bluez5 \
"

# Inherit the default LIBC features superset from OE-core
DISTRO_FEATURES += "${DISTRO_FEATURES_LIBC}"

VIRTUAL-RUNTIME_graphical_init_manager = "xserver-xfce-init"

VIRTUAL-RUNTIME_dev_manager ?= "udev"
VIRTUAL-RUNTIME_mountpoint ?= "busybox"

# For qemu images
VIRTUAL-RUNTIME_keymaps = ""

LVRT_USER = "lvuser"
LVRT_GROUP = "ni"
OPENVPN_GROUP = "openvpn"
OPENVPN_USER = "openvpn"

# If SDKMACHINE is not defined, assume toolchain is created
# for a target device and use kernel version 3.14.3 to take
# advantage of ARM optimizations
OLDEST_KERNEL = "${@['3.14.3','2.6.32'][d.getVar('SDKMACHINE', True) != None]}"

PREFERRED_PROVIDER_python="python-zip"

PREFERRED_PROVIDER_virtual/fftw = "fftwf"

PREFERRED_PROVIDER_jpeg = "jpeg"
PREFERRED_PROVIDER_jpeg-native = "jpeg-native"
PREFERRED_PROVIDER_gnome-icon-theme = "gnome-icon-theme"
PREFERRED_PROVIDER_iasl = "acpica"
PREFERRED_PROVIDER_make-native = "make-native"
PREFERRED_PROVIDER_opencv-samples = "opencv-samples"
PREFERRED_PROVIDER_virtual/java-initial-native = "cacao-initial-native"
PREFERRED_PROVIDER_virtual/java-native = "cacao-native"

# meta-openstack uses overly-restrictive version preferences, we
# avoid build warnings by relaxing them
PREFERRED_VERSION_python-futures = "3.%"
PREFERRED_VERSION_python-sqlalchemy = "1.%"
PREFERRED_VERSION_python-greenlet = "0.4.%"

PREFERRED_VERSION_sepolgen = "1.2.1+git%"

GCCVERSION = "4.9.3"

LOCALE_UTF8_IS_DEFAULT = "0"

IMAGE_LINGUAS ?= "en-us.utf-8 en-us"

# Root URI of all NILRT feeds
NILRT_FEEDS_URI                        ?= "http://download.ni.com/ni-linux-rt/feeds"

# Feed URI specific to this machine and release of NILRT
NILRT_MACHINE_FEED_URI                  = "${NILRT_FEEDS_URI}/${NILRT_RELEASE_NAME}/${MACHINE_ARCH}"

# Name of subfeeds under root URI
NILRT_SUBFEED_NAMES                     = "NIOE NIOEExtras NI"

# URIs to each subfeed
# Migration is special: there's only one feed that's release, version,
#  and machine agnostic, so NILRT_RELEASE_NAME=Migration and
#  MACHINE_ARCH=all for this subfeed
NIOE_SUBFEED_URI                       ?= "${NILRT_MACHINE_FEED_URI}/main"
NIOEExtras_SUBFEED_URI                 ?= "${NILRT_MACHINE_FEED_URI}/extra"
NI_SUBFEED_URI                         ?= "${NILRT_MACHINE_FEED_URI}/ni"
NIOEMigration_SUBFEED_URI              ?= "${NILRT_FEEDS_URI}/Migration/all"

# Available arches in each subfeed
NILRT_SUBFEED_ARCHES                    = "all ${MACHINE} ${TUNE_PKGARCH}"

# Tell OE to build image from feeds
BUILD_IMAGES_FROM_FEEDS                 = "1"

# Get NIOE packages from local deploy feed and NI packages from web feed
IPK_NIOE_SUBFEED_URI                   ?= "file://${DEPLOY_DIR_IPK}"
IPK_NI_SUBFEED_URI                     ?= "${NI_SUBFEED_URI}"

# Configure feed URIs for image builds
IPK_FEED_URIS += "\
    NIOE-all##${IPK_NIOE_SUBFEED_URI}/all \
    NIOE-${MACHINE}##${IPK_NIOE_SUBFEED_URI}/${MACHINE} \
    NIOE-${TUNE_PKGARCH}##${IPK_NIOE_SUBFEED_URI}/${TUNE_PKGARCH} \
    NI-all##${IPK_NI_SUBFEED_URI}/all \
    NI-${MACHINE}##${IPK_NI_SUBFEED_URI}/${MACHINE} \
    NI-${TUNE_PKGARCH}##${IPK_NI_SUBFEED_URI}/${TUNE_PKGARCH} \
"

# Base URI to NI Linux RT's Git repository
NILRT_GIT ?= "git://github.com/ni"

NILRT_ARM_MIGRATION_SUPPORTED_DEVICES = "\
    762F 76D3 76D6 76F2 7740 7741 7742 7743 7744 774C \
    774E 775E 77D4 77D5 77D6 7885 77AC 77B1 77B2 \
"
